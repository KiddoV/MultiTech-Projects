;
;	Author: 		Viet Ho
;	Description: 	Teraterm Script For EUID Write of XDOTs-ALL Freq.
;					Cannot Stand-alone run!
;					Only work with other program that passes PARAMETERS to this macro.
;

show 0
timeout = 5
mpause 1000

:GET_SET_VARS
currentLotCode = param3
runUser = param4
sprintf2 writeResultLogFilePath 'Z:\XDOT\Logging\writing\%s.log' currentLotCode

strsplit param2 ','		;Get all datas by 1 param

writeFirmwareVers = groupmatchstr8
isSkipWriteFw = 0

firmwareVers = ''
strcompare writeFirmwareVers 'v3.2.1'
if result=0 firmwareVers = 'Firmware : 3.2.1-mbed51101'
strcompare writeFirmwareVers 'v3.1.0'
if result=0 firmwareVers = 'Firmware : 3.1.0-mbed161'
strcompare writeFirmwareVers '-Skip-'
if result=0 isSkipWriteFw = 1

xdotMainPort = groupmatchstr2	;101
xdotBreakPort = groupmatchstr3	;11
sprintf2 xdotDriveLoc '%s:\' groupmatchstr4
strcompare groupmatchstr5 'singleWrite'
isSingleWrite = result
freq = groupmatchstr6

strcompare freq 'IN865'
if result=0 debugFirmwareVers = '3.2.1-debug-mbed51101'
if result<>0 debugFirmwareVers = '3.0.2-debug-mbed144'

outDFreq = ''
deadNode = groupmatchstr7
outDeadNode = ''
writeStatus = ''
fail = 0
connFail = 0
sprintf2 portNumberFailureMsg 'PORT %s FAILURE' xdotMainPort
sprintf2 portNumberTitleMsg 'PORT %s' xdotMainPort
binFileLoc = ''

if isSkipWriteFw=1 goto CONNECT

outDeadNode = 'N/A'
outDFreq = 'N/A'

strcompare writeFirmwareVers 'v3.2.1'
if result=0 then
	strcompare freq 'AS923'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-AS923-mbed-os-5.11.1.bin'
	strcompare freq 'AS923-JAPAN'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-AS923_JAPAN-mbed-os-5.11.1.bin'
	strcompare freq 'AU915'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-AU915-mbed-os-5.11.1.bin'
	strcompare freq 'EU868'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-EU868-mbed-os-5.11.1.bin'
	strcompare freq 'IN865'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-IN865-mbed-os-5.11.1.bin'
	strcompare freq 'KR920'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-KR920-mbed-os-5.11.1.bin'
	strcompare freq 'RU864'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-RU864-mbed-os-5.11.1.bin'
	strcompare freq 'US915'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-US915-mbed-os-5.11.1.bin'
endif

strcompare writeFirmwareVers 'v3.1.0'
if result=0 then
	strcompare freq 'AS923'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.1.0-AS923-mbed-os-5.7.7.bin'
	strcompare freq 'AS923-JAPAN'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.1.0-AS923_JAPAN-mbed-os-5.7.7.bin'
	strcompare freq 'AU915'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.1.0-AU915-mbed-os-5.7.7.bin'
	strcompare freq 'EU868'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.1.0-EU868-mbed-os-5.7.7.bin'
	strcompare freq 'IN865'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.1.0-IN865-mbed-os-5.7.7.bin'
	strcompare freq 'KR920'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.1.0-KR920-mbed-os-5.7.7.bin'
	strcompare freq 'RU864'
	if result=0 binFileLoc = ''
	strcompare freq 'US915'
	if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.1.0-US915-mbed-os-5.7.7.bin'
endif

:CONNECT
sprintf2 cmdStr '/C=%s' xdotMainPort
connect cmdStr
if result=1 goto connection_failed

:CHECK_CONNECTION
mpause 500
sendln 'at'
waitln 'OK'
if result=1 then
    if isSingleWrite = 0 then
        statusbox 'CONNECTED SUCCESSFULLY' 'PASSED1'
    	mpause 300
		closesbox
	endif 
endif
if result=0 then
	:connection_failed
    closett
    writeStatus = 'FAILED CONNECTING TO PORT'
    statusbox writeStatus portNumberFailureMsg
    connFail=0
    goto RESULT_LOGGING
    end
endif

:CHECK_CURRENT_PROGRAM
flushrecv
sendln 'ati'
wait debugFirmwareVers 'OK'
if result>1 then
    yesnobox 'CURRENT PROGRAM IS NOT IN DEBUG MODE!\nDO YOU WANT TO RE-PROGRAM TO DEBUG?\nNOTE: THIS ACTION WILL ERASE ALL SETTINGS ON XDOT' portNumberTitleMsg 1
    if result=0 then
        writeStatus = 'PROGRAM FAILED - NOT IN DEBUG'
        statusbox writeStatus portNumberFailureMsg
        goto WRITE_RESULT_DATA
    endif
    if result=1 goto REPROGRAM_TO_DEBUG
elseif result=1 then
	goto BEGIN_WRITE_NODE
endif

:REPROGRAM_TO_DEBUG
strcompare freq 'IN865'
if result=0 sprintf2 cmd 'cmd /c copy C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-US915-mbed-os-5.11.1-debug.bin %s' xdotDriveLoc
if result<>0 sprintf2 cmd 'cmd /c copy C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.0.2-US915-mbed-os-5.4.7-debug.bin %s' xdotDriveLoc
exec cmd
sprintf2 title 'PORT %s IS PROGRAMMING' xdotMainPort
statusbox 'Transfering bin file........' title
mpause 16500
closesbox
:send_break_debug
disconnect
mpause 500
sprintf2 cmd1 '/C=%s' xdotBreakPort
connect cmd1
flushrecv
setbaud 9600
mpause 3000
sendbreak
disconnect
mpause 500
sprintf2 cmd2 '/C=%s' xdotMainPort
connect cmd2
flushrecv
setbaud 115200
mpause 1000
sendln 'at'
wait 'OK'
mpause 500
sendln 'at&f'
wait 'OK'
mpause 500
sendln 'at&w'
wait 'OK'
goto CHECK_CURRENT_PROGRAM

:BEGIN_WRITE_NODE
flushrecv
statusbox deadNode portNumberTitleMsg
mpause 500
sprintf2 cmd 'at+di=%s' deadNode
sendln cmd
wait 'OK'
mpause 500
sprintf2 cmd 'at+dfreq=%s' freq
sendln cmd
waitln 'OK' 'ERROR'
if result=2 goto invalid_dfreq
mpause 500
sendln 'at&wp'
wait "OK"
mpause 500
sendln 'atz'
wait "OK"
if isSingleWrite = 0 then
     statusbox 'WRITE NODE SUCCESSFULLY' 'PASSED2'
     mpause 300
	 closesbox
endif 

if isSkipWriteFw=1 goto recheck_write_input

:REPROGRAM
strlen binFileLoc
if result < 5 goto binfile_failed
sprintf2 cmd 'cmd /c copy %s %s' binFileLoc xdotDriveLoc
exec cmd
sprintf2 title 'PORT %s IS PROGRAMMING' xdotMainPort
statusbox 'Transfering bin file........' title
mpause 16500
closesbox
:send_break
disconnect
mpause 500
sprintf2 cmd1 '/C=%s' xdotBreakPort
connect cmd1
flushrecv
setbaud 9600
mpause 3000
sendbreak
disconnect
mpause 500
sprintf2 cmd2 '/C=%s' xdotMainPort
connect cmd2
flushrecv
setbaud 115200
mpause 1000
sendln 'at'
wait 'OK'
mpause 500
sendln 'at&f'
wait 'OK'
mpause 500
sendln 'at&w'
wait 'OK'
flushrecv
:recheck_program
timeout = 3
mpause 500
sendln 'ati'
wait firmwareVers
if result=1 then
    if isSingleWrite = 0 then
        statusbox 'RE-PROGRAM PASSED' 'PASSED3'
    	mpause 300
		closesbox
	endif 
endif
if result=0 then
    fail=fail+1
    if fail < 3 then
      goto REPROGRAM
    endif
	:reprogram_failed
	writeStatus = 'FAILED TO RE-PROGRAM'
    statusbox writeStatus portNumberFailureMsg
	fail = 0
	mpause 300
	goto RESULT_LOGGING
endif

:recheck_write_input
flushrecv
;;;Fix bug where Teraterm is not sending 'at+freq' at all
mpause 500
sendln 'at'
wait 'OK'
;mpause 500
;sendln 'at+freq'
;waitregex '[A-Z]+([0-9]{3})'
;outFreq = inputstr
mpause 500
sendln 'at+freq'
waitln freq
outFreq = inputstr
if result=0 then
    writeStatus = 'WRONG FREQUENCY'
    statusbox writeStatus portNumberFailureMsg
    mpause 300
	goto RESULT_LOGGING
endif
;mpause 500
;sendln 'at+dfreq'
;waitregex '[A-Z]+([0-9]{3})'
;outDFreq = inputstr
mpause 500
sendln 'at+dfreq'
wait freq
outDFreq = inputstr
if result=0 then
    writeStatus = 'WRONG DEFAULT FREQUENCY'
    statusbox writeStatus portNumberFailureMsg
    mpause 300
	goto RESULT_LOGGING
endif
mpause 500
sendln 'at+di?'
waitln '0' '8'
outDeadNode = inputstr
mpause 500
sendln 'at+di?'
waitregex '([0-9a-fA-F]{2}[:-]){7}([0-9a-fA-F]{2})' '00-00-00-00-00-00-00-00'
for i 1 17
	strreplace inputstr 1 '-' ''
next
if result=1 then
    strcompare inputstr deadNode
    if result <> 0 then
        writeStatus = 'INVALID EUID - NOT MATCH'
        statusbox writeStatus portNumberFailureMsg
		mpause 300
		goto RESULT_LOGGING
	endif
endif
if result=2 then
    writeStatus = 'INVALID EUID - ALL ZERO'
    statusbox writeStatus portNumberFailureMsg
	mpause 300
	goto RESULT_LOGGING
endif

:DONE
mpause 500
sprintf2 msg1 '%s EUID WRITE COMPLETE' portNumberTitleMsg
sprintf2 msg2 'PORT %s PASSED' xdotMainPort
statusbox msg1 msg2
writeStatus = 'PASSED'

:RESULT_LOGGING
mpause 500
date = ''
time = ''
logStr = ''
getdate date "%Y/%m/%d"
getdate time "%H:%M:%S"
;;sprintf2 logStr '%s, %s, COM: %s, STATUS: %s, DeadNode: %s, ReadNode: %s, Freq: %s, D-Freq: %s, BK-COM: %s' date time xdotMainPort writeStatus deadNode outDeadNode freq outDFreq xdotBreakPort
sprintf2 logStr '%s,%s,%s,%s,%s,%s,%s,%s,%s,%s' date time xdotMainPort writeStatus deadNode outDeadNode freq outDFreq xdotBreakPort runUser
filesearch writeResultLogFilePath
if result = 0 then
   filecreate fhandle writeResultLogFilePath
else
	fileopen fhandle writeResultLogFilePath 1
	filewrite fhandle #13#10	;;Make a line feed
endif
filewrite fhandle logStr
fileclose fhandle

:WRITE_RESULT_DATA
mpause 500
writeStr = ''

foldersearch 'C:\V-Projects\XDot-Controller\TEMP-DATA'
if result=0 foldercreate 'C:\V-Projects\XDot-Controller\TEMP-DATA'
sprintf2 cacheFileLoc 'C:\V-Projects\XDot-Controller\TEMP-DATA\%s.dat' xdotMainPort
filesearch cacheFileLoc
if result = 0 filecreate fhandle cacheFileLoc

fileopen fhandle cacheFileLoc 0	;0 for overwrite
sprintf2 writeStr 'WRITE|%s|%s|%s|%s|%s' xdotMainPort deadNode writeStatus freq writeFirmwareVers
filewrite fhandle writeStr
fileclose fhandle

end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
:invalid_dfreq
writeStatus = 'INPUT INVALID DFREQ'
statusbox writeStatus portNumberFailureMsg
mpause 300
goto RESULT_LOGGING

:binfile_failed
writeStatus = 'BIN FILE NOT FOUND'
statusbox writeStatus portNumberFailureMsg
mpause 300
goto RESULT_LOGGING