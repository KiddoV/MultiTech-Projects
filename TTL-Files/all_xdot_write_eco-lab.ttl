;
;	Author: 		Viet Ho
;	Description: 	Teraterm Script For EUID Write of XDOTs-ALL Freq.
;					Cannot Stand-alone run!
;					Only work with other program that passes PARAMETERS to this macro.
;

show 0
timeout = 5
mpause 1000

:GET_SET_VARS
currentLotCode = param3
sprintf2 writeResultLogFilePath 'Z:\XDOT\Logging\writing\%s.log' currentLotCode

strsplit param2 ','		;Get all datas by 1 param

writeFirmwareVers = groupmatchstr8
strcompare writeFirmwareVers 'v3.0.2'
if result=0 firmwareVers = 'Firmware : 3.0.2-mbed144'
strcompare writeFirmwareVers 'v3.2.1'
if result=0 firmwareVers = 'Firmware : 3.2.1-mbed51101'

debugFirmwareVers = '3.0.2-debug-mbed144'
xdotMainPort = groupmatchstr2	;101
xdotBreakPort = groupmatchstr3	;11
sprintf2 xdotDriveLoc '%s:\' groupmatchstr4
strcompare groupmatchstr5 'singleWrite'
isSingleWrite = result
freq = groupmatchstr6
outDFreq = 'N/A'
allID = groupmatchstr7
outDeadNode = 'N/A'
outAppKey = 'N/A'
outUuid = 'N/A'
writeStatus = 'N/A'
fail = 0
connFail = 0
sprintf2 portNumberFailureMsg 'PORT %s FAILURE' xdotMainPort
sprintf2 portNumberTitleMsg 'PORT %s' xdotMainPort'
flushrecv
strcompare writeFirmwareVers 'v3.0.2'
if result=0 then
    strcompare freq 'US915'
    if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.0.2-US915-mbed-os-5.4.7.bin'
endif
strcompare writeFirmwareVers 'v3.2.1'
if result=0 then
    strcompare freq 'EU868'
    if result=0 binFileLoc = 'C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.2.1-EU868-mbed-os-5.11.1.bin'
endif

;;;Split data
strsplit allID '|'
if isSingleWrite != 0 then
	deadNode = groupmatchstr2
	serialNum = groupmatchstr3
else
	deadNode = groupmatchstr3
	serialNum = groupmatchstr2	
endif
appKey = groupmatchstr4
uuid = groupmatchstr5

;:WRITE_BLANK_LINE
;isFirstNode = -1
;strcompare xdotMainPort '101'
;if result=0 isFirstNode = result
;strcompare xdotMainPort '109'
;if result=0 isFirstNode = result
;strcompare xdotMainPort '117'
;if result=0 isFirstNode = result
;if isFirstNode = 0 then
;   fileopen fhandle 'C:\Vbtest\mtxdot\xdot_EUI_and_FIRMWARE_results_info.txt' 1
;   filewrite fhandle #13#10
;   fileclose fhandle
;endif

:CONNECT
sprintf2 cmdStr '/C=%s' xdotMainPort
connect cmdStr
if result=1 goto connection_failed

:CHECK_CONNECTION
mpause 500
sendln 'at'
waitln 'OK'
if result=1 then
    if isSingleWrite = 0 then
        statusbox 'CONNECTED SUCCESSFULLY' 'PASSED1'
    	mpause 300
		closesbox
	endif 
endif
if result=0 then
	:connection_failed
    closett
    writeStatus = 'FAILED CONNECTING TO PORT'
    statusbox writeStatus portNumberFailureMsg
    connFail=0
    goto RESULT_LOGGING
    end
endif

:CHECK_CURRENT_PROGRAM
sendln 'ati'
wait debugFirmwareVers 'OK'
if result>1 then
    yesnobox 'CURRENT PROGRAM IS NOT IN DEBUG MODE!\nDO YOU WANT TO RE-PROGRAM TO DEBUG?\nNOTE: THIS ACTION WILL ERASE ALL SETTINGS ON XDOT' portNumberTitleMsg 1
    if result=0 then
        writeStatus = 'PROGRAM FAILED - NOT IN DEBUG'
        statusbox writeStatus portNumberFailureMsg
        goto WRITE_RESULT_DATA
    endif
    if result=1 goto REPROGRAM_TO_DEBUG
elseif result=1 then
	goto BEGIN_WRITE_NODE
endif

:REPROGRAM_TO_DEBUG
sprintf2 cmd 'cmd /c copy C:\V-Projects\XDot-Controller\BIN-Files\xdot-firmware-3.0.2-US915-mbed-os-5.4.7-debug.bin %s' xdotDriveLoc
exec cmd
sprintf2 title 'PORT %s IS PROGRAMMING' xdotMainPort
statusbox 'Transfering bin file........' title
mpause 16500
closesbox
:send_break_debug
disconnect
mpause 500
sprintf2 cmd1 '/C=%s' xdotBreakPort
connect cmd1
flushrecv
setbaud 9600
mpause 3000
sendbreak
disconnect
mpause 500
sprintf2 cmd2 '/C=%s' xdotMainPort
connect cmd2
flushrecv
setbaud 115200
mpause 1000
sendln 'at'
wait 'OK'
mpause 500
sendln 'at&f'
wait 'OK'
mpause 500
sendln 'at&w'
wait 'OK'
goto CHECK_CURRENT_PROGRAM

:BEGIN_WRITE_NODE
statusbox deadNode portNumberTitleMsg
mpause 500
sprintf2 cmd 'at+di=%s' deadNode
sendln cmd
wait 'OK'
mpause 500
statusbox appKey portNumberTitleMsg
sprintf2 cmd 'at+ni=2,%s' appKey
sendln cmd
wait 'OK'
mpause 500
statusbox uuid portNumberTitleMsg
sprintf2 cmd 'at+nk=2,%s' uuid
sendln cmd
wait 'OK'
mpause 500
sprintf2 cmd 'at+dfreq=%s' freq
sendln cmd
wait 'OK'

mpause 500
sendln 'at&wp'
wait "OK"
mpause 500
sendln 'at&f'
wait "OK"
mpause 500
sendln 'at&w'
wait "OK"
mpause 500
sendln 'atz'
wait "OK"
if isSingleWrite = 0 then
     statusbox 'WRITE ALL NODE IDs SUCCESSFULLY' 'PASSED2'
     mpause 300
	 closesbox
endif

:REPROGRAM
sprintf2 cmd 'cmd /c copy %s %s' binFileLoc xdotDriveLoc
exec cmd
sprintf2 title 'PORT %s IS PROGRAMMING' xdotMainPort
statusbox 'Transfering bin file........' title
mpause 16500
closesbox
:send_break
disconnect
mpause 500
sprintf2 cmd1 '/C=%s' xdotBreakPort
connect cmd1
flushrecv
setbaud 9600
mpause 3000
sendbreak
disconnect
mpause 500
sprintf2 cmd2 '/C=%s' xdotMainPort
connect cmd2
flushrecv
setbaud 115200
mpause 1000
sendln 'at'
wait 'OK'
mpause 500
sendln 'at&f'
wait 'OK'
mpause 500
sendln 'at&w'
wait 'OK'
flushrecv
:recheck_program
timeout = 3
mpause 500
sendln 'ati'
wait firmwareVers
if result=1 then
    if isSingleWrite = 0 then
        statusbox 'RE-PROGRAM PASSED' 'PASSED3'
    	mpause 300
		closesbox
	endif 
endif
if result=0 then
    fail=fail+1
    if fail < 3 then
      goto REPROGRAM
    endif
	:reprogram_failed
	writeStatus = 'FAILED TO RE-PROGRAM'
    statusbox writeStatus portNumberFailureMsg
	fail = 0
	mpause 300
	goto RESULT_LOGGING
endif

:recheck_write_input
flushrecv
mpause 500
sendln 'at+freq'
waitregex '[A-Z]+([0-9]{3})'
outFreq = inputstr
mpause 500
sendln 'at+freq'
wait freq
if result=0 then
    writeStatus = 'WRONG FREQUENCY'
    statusbox writeStatus portNumberFailureMsg
    mpause 300
	goto RESULT_LOGGING
endif
mpause 500
sendln 'at+dfreq'
;waitregex '[A-Z]+([0-9]{3})'
waitln freq
outDFreq = inputstr
mpause 500
sendln 'at+dfreq'
wait freq
if result=0 then
    writeStatus = 'WRONG DEFAULT FREQUENCY'
    statusbox writeStatus portNumberFailureMsg
    mpause 300
	goto RESULT_LOGGING
endif
mpause 500
sendln 'at+di?'
waitln '0' '8'
outDeadNode = inputstr
mpause 500
sendln 'at+di?'
waitregex '([0-9a-fA-F]{2}[:-]){7}([0-9a-fA-F]{2})' '00-00-00-00-00-00-00-00'
for i 1 17
	strreplace inputstr 1 '-' ''
next
if result=1 then
    strcompare inputstr deadNode
    if result <> 0 then
        writeStatus = 'INVALID NODE ID - NOT MATCH'
        statusbox writeStatus portNumberFailureMsg
		mpause 300
		goto RESULT_LOGGING
	endif
endif
if result=2 then
    writeStatus = 'INVALID NODE ID - ALL ZERO'
    statusbox writeStatus portNumberFailureMsg
	mpause 300
	goto RESULT_LOGGING
endif
mpause 500
sendln 'at+ni?'
waitln '0' '8'
outAppKey = inputstr
mpause 500
sendln 'at+ni?'
waitregex '([0-9a-fA-F]{2}[:-]){7}([0-9a-fA-F]{2})' '00-00-00-00-00-00-00-00'
for i 1 17
	strreplace inputstr 1 '-' ''
next
if result=1 then
    strcompare inputstr appKey
    if result <> 0 then
        writeStatus = 'INVALID APP KEY - NOT MATCH'
        statusbox writeStatus portNumberFailureMsg
		mpause 300
		goto RESULT_LOGGING
	endif
endif
if result=2 then
    writeStatus = 'INVALID APP KEY - ALL ZERO'
    statusbox writeStatus portNumberFailureMsg
	mpause 300
	goto RESULT_LOGGING
endif
mpause 500
sendln 'at+nk?'
waitln '0'
outUuid = inputstr
mpause 500
sendln 'at+nk?'
waitregex '([0-9a-fA-F]{2}[.:-]){15}([0-9a-fA-F]{2})' '00.00.00.00.00.00.00.00.00.00.00.00.00.00.00.00'
for i 1 32
	strreplace inputstr 1 '\.' ''
next
if result=1 then
    strcompare inputstr uuid
    if result <> 0 then
        writeStatus = 'INVALID UUID - NOT MATCH'
        statusbox writeStatus portNumberFailureMsg
		mpause 300
		goto RESULT_LOGGING
	endif
endif
if result=2 then
    writeStatus = 'INVALID UUID - ALL ZERO'
    statusbox writeStatus portNumberFailureMsg
	mpause 300
	goto RESULT_LOGGING
endif

:DONE
mpause 500
sprintf2 msg1 '%s EUID WRITE COMPLETE' portNumberTitleMsg
sprintf2 msg2 'PORT %s PASSED' xdotMainPort
statusbox msg1 msg2
writeStatus = 'PASSED'

:RESULT_LOGGING
mpause 500
date = ''
time = ''
logStr = ''
getdate date "%Y/%m/%d"
getdate time "%H:%M:%S"
;sprintf2 logStr '%s, %s, COM: %s, STATUS: %s, DeadNode: %s, ReadNode: %s, Freq: %s, D-Freq: %s, UUID: %s, ReadUUID: %s, AppKey: %s, ReadAppKey: %s,BK-COM: %s' date time xdotMainPort writeStatus deadNode outDeadNode freq outDFreq uuid outUuid appKey outAppKey xdotBreakPort
sprintf2 logStr '%s,%s,%s,%s,%s,%s,%s,%s,%s,%s,%s,%s,%s' date time xdotMainPort writeStatus deadNode outDeadNode freq outDFreq uuid outUuid appKey outAppKey xdotBreakPort

filesearch writeResultLogFilePath
if result = 0 then
   filecreate fhandle writeResultLogFilePath
else
	fileopen fhandle writeResultLogFilePath 1
	filewrite fhandle #13#10	;;Make a line feed
endif
filewrite fhandle logStr
fileclose fhandle

:WRITE_RESULT_DATA
mpause 500
writeStr = ''

foldersearch 'C:\V-Projects\XDot-Controller\TEMP-DATA'
if result=0 foldercreate 'C:\V-Projects\XDot-Controller\TEMP-DATA'
sprintf2 cacheFileLoc 'C:\V-Projects\XDot-Controller\TEMP-DATA\%s.dat' xdotMainPort
filesearch cacheFileLoc
if result = 0 filecreate fhandle cacheFileLoc

fileopen fhandle cacheFileLoc 0	;0 for overwrite
sprintf2 allID '%s,%s,%s,%s' serialNum deadNode appKey uuid
sprintf2 writeStr 'WRITE|%s|%s|%s|%s|%s' xdotMainPort allID writeStatus freq writeFirmwareVers
filewrite fhandle writeStr
fileclose fhandle

end